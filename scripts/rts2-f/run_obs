# expected to receive output of rts2-seltest command
# assumes rts2-f-env script was called, so important variables
# are set up

#setenv tar_id $1
#setenv tar_id `$RTS2/bin/rts2-seltest --filter-file='KCAM:/Realtime/lib/filt.dat' --filter-aliases /home/petr/rts2-sys/etc/rts2/aliases`

set tar_id=`$RTS2/bin/rts2-xmlrpcclient --config $XMLRPCCON -G SEL.next_id`
if ( $? != 0 || $tar_id <= 0 ) then
	echo `date` selector is not running, executing seltest to select next target
	set tar_id=`$RTS2/bin/rts2-seltest --filter-file='KCAM:/Realtime/lib/filt.dat' --filter-aliases /home/petr/rts2-sys/etc/rts2/aliases --targetid`
	echo `date` selected $tar_id
endif

$RTS2/bin/rts2-xmlrpcclient --config $XMLRPCCON -c SEL.next

if ( -r $lasttarget ) then
	setenv last_tar_id `cut -f1 -d' ' $lasttarget`
	setenv obs_id `cut -f2 -d' ' $lasttarget`
else
	setenv last_tar_id -1
endif

if ( $last_tar_id != $tar_id ) then
	setenv obs_id `$RTS2/bin/rts2-target --slew $tar_id`
	echo $tar_id $obs_id > $lasttarget
endif

set ti=`rts2-targetinfo $tar_id`

set name=`rts2-targetinfo --name $tar_id | sed -e 's# #_#g' -e 's#[^A-Za-z0-9+_.-]#X#g'`
set pi=`rts2-targetinfo --pi $tar_id`
set program=`rts2-targetinfo --program $tar_id`

set ra=`echo $ti | awk '{ print $3 }'`
set dec=`echo $ti | awk '{ print $4 }'`

echo "Starting observation of target $name, ID $tar_id, pi $pi, program $program"
echo "Observation ID is $obs_id"

$RTS2/bin/rts2-xmlrpcclient --config $XMLRPCCON -c "SEL.observation $tar_id $obs_id"

if ( $notmove == 1 ) then
  	echo "move to coordinates ignored"

	sleep 3
else
	if ( $last_tar_id != $tar_id ) then
		echo -n "sending coordinates $ra $dec 2000.0 .. "
		tele coords $ra $dec 2000.0

		sleep 3

		tele enable

		echo "on coordinates"

		# mark end of slew
		$RTS2/bin/rts2-target --observe $obs_id

		echo "marked end of telescope movement"
	endif
endif
	
object "$name"
setcom kepccd Object "$name"
tele pi "$pi"
tele program "$program"

# translate observation to observation file..

rm -f /tmp/$obs_id

$RTS2/bin/rts2-targetinfo --parse KCAM $tar_id | xsltproc /home/petr/rts2/scripts/flwo_tcs.xsl - > /tmp/$obs_id

source /tmp/$obs_id

# end it
$RTS2/bin/rts2-target --end $obs_id

rm -f /tmp/$obs_id

$RTS2/bin/rts2-xmlrpcclient --config $XMLRPCCON -c SEL.next

echo `date` "observation $obs_id of object $name ($ra $dec) ends"
